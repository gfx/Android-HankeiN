apply plugin: 'com.android.application'
apply plugin: 'com.github.gfx.android.powerassert'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.github.gfx.ribbonizer'
apply plugin: 'com.jakewharton.hugo'

rootProject.ext.properties.each { key, value ->
    if (!project.ext.has(key)) {
        project.ext.set(key, value)
    }
}

final APPLICATION_ID = "com.github.gfx.hankei_n"

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.0'
    dataBinding.enabled = true
    defaultConfig {
        applicationId APPLICATION_ID
        minSdkVersion 16
        targetSdkVersion 24
        def version = readVersion(rootProject.file("VERSION"))
        versionCode version.toVersionCode()
        versionName version.toVersionName()
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }
    compileOptions {
        encoding = "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    signingConfigs {
        debug {
            storeFile file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
        release {
            if (project.ext.has('ReleaseStoreFile')) {
                println("with release signing");

                storeFile file(project.ext.ReleaseStoreFile.replaceAll("~", System.getenv("HOME")))
                storePassword project.ext.ReleaseStorePassword
                keyAlias project.ext.ReleaseKeyAlias
                keyPassword project.ext.ReleaseKeyPassword
            } else {
                println("without release signing");

                // Just to build it in CI (Google API won't work with the debug keystore)
                storeFile rootProject.file("debug.keystore")
                storePassword "android"
                keyAlias "androiddebugkey"
                keyPassword "android"
            }
        }
    }
    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"

            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    lintOptions {
        disable 'InvalidPackage'
        disable 'TypographyDashes'
    }
    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
    productFlavors {
    }
}


dependencies {
    compile 'com.android.support:appcompat-v7:24.1.0'
    compile 'com.android.support:cardview-v7:24.1.0'
    compile 'com.android.support:recyclerview-v7:24.1.0'
    compile 'com.android.support:design:24.1.0'
    compile 'com.android.support:customtabs:24.1.0'
    compile 'com.android.support:support-annotations:24.1.0'
    compile 'com.google.android.gms:play-services-maps:9.2.1'
    compile 'com.google.android.gms:play-services-location:9.2.1'
    compile 'com.google.android.gms:play-services-places:9.2.1'
    compile 'com.google.android.gms:play-services-analytics:9.2.1'
    compile 'com.google.firebase:firebase-crash:9.2.0'
    compile 'com.google.code.findbugs:jsr305:3.0.1'
    compile 'io.reactivex:rxjava:1.1.7'
    compile 'com.jakewharton.rxbinding:rxbinding:0.4.0'
    compile 'com.cookpad.android.rxt4a:rxt4a:1.0.0'
    compile 'com.google.code.gson:gson:2.7'
    compile 'com.jakewharton.timber:timber:4.1.1'
    compile 'com.google.dagger:dagger:2.0.2'
    apt 'com.google.dagger:dagger-compiler:2.0.2'
    provided 'javax.annotation:jsr250-api:1.0'
    apt 'com.github.gfx.android.orma:orma-processor:2.5.2'
    compile 'com.github.gfx.android.orma:orma:2.5.2'
    debugCompile 'com.facebook.stetho:stetho:1.3.1'
    testCompile 'com.github.gfx.android.robolectricinstrumentation:robolectric-instrumentation:3.1.0'
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
}

apply plugin: 'com.google.gms.google-services'
