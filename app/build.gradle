apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'com.android.databinding'
apply plugin: 'com.github.gfx.android.powerassert'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.github.gfx.ribbonizer'

rootProject.ext.properties.each { key, value ->
    if (!project.ext.has(key)) {
        project.ext.set(key, value)
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.1'

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 22

        def version = readVersion(rootProject.file("VERSION"))
        versionCode version.toVersionCode()
        versionName version.toVersionName()

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        encoding = "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        release {
            storeFile file(project.ext.ReleaseStoreFile.replaceAll("~", System.getenv("HOME")))
            storePassword project.ext.ReleaseStorePassword
            keyAlias project.ext.ReleaseKeyAlias
            keyPassword project.ext.ReleaseKeyPassword
        }
    }

    buildTypes {
        debug {
            debuggable true

            resValue "string", "google_api_key", GoogleApiKey_Debug
            resValue "string", "ga_tracking_id", GA_TrackingId_Debug
        }

        release {
            resValue "string", "google_api_key", GoogleApiKey_Release
            resValue "string", "ga_tracking_id", GA_TrackingId_Release

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'

            if (signingConfigs.release.storeFile.exists()) {
                signingConfig signingConfigs.release
            } else {
                logger.info(
                        "keystore ${signingConfigs.release.storeFile} does not exist. Release signing is ignored.")
            }
        }

        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
    }

    lintOptions {
        disable 'InvalidPackage'
        disable 'TypographyDashes'
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }
}


dependencies {
    compile 'com.android.support:appcompat-v7:23.1.0'
    compile 'com.android.support:cardview-v7:23.1.0'
    compile 'com.android.support:design:23.1.0'
    compile 'com.android.support:support-annotations:23.1.0'
    compile 'com.google.android.gms:play-services-maps:8.1.0'
    compile 'com.google.android.gms:play-services-location:8.1.0'
    compile 'com.google.android.gms:play-services-analytics:8.1.0'
    compile 'com.google.android.gms:play-services-ads:8.1.0'
    compile 'com.google.code.findbugs:jsr305:3.0.0'
    compile 'io.reactivex:rxjava:1.0.14'
    compile 'com.cookpad.android.rxt4a:rxt4a:1.0.0'
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'com.jakewharton.timber:timber:3.1.0'
    compile 'com.google.dagger:dagger:2.0.1'
    apt 'com.google.dagger:dagger-compiler:2.0.1'
    provided 'javax.annotation:jsr250-api:1.0'
    apt 'com.android.databinding:compiler:1.0-rc2'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.0') {
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    }
}
