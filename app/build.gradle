apply plugin: 'com.android.application'
apply plugin: 'com.github.gfx.android.powerassert'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.github.gfx.ribbonizer'

rootProject.ext.properties.each { key, value ->
    if (!project.ext.has(key)) {
        project.ext.set(key, value)
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.3'

    dataBinding.enabled = true

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 23

        def version = readVersion(rootProject.file("VERSION"))
        versionCode version.toVersionCode()
        versionName version.toVersionName()

        testInstrumentationRunner 'org.robolectric.RobolectricTestRunner'
    }

    compileOptions {
        encoding = "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        release {
            storeFile file(project.ext.ReleaseStoreFile.replaceAll("~", System.getenv("HOME")))
            storePassword project.ext.ReleaseStorePassword
            keyAlias project.ext.ReleaseKeyAlias
            keyPassword project.ext.ReleaseKeyPassword
        }
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"

            manifestPlaceholders = [GOOGLE_API_KEY:GoogleApiKey_Debug]
            buildConfigField "String", "GA_TRACKING_ID", '"' + GA_TrackingId_Debug + '"'

            shrinkResources false
        }

        release {
            manifestPlaceholders = [GOOGLE_API_KEY:GoogleApiKey_Release]
            buildConfigField "String", "GA_TRACKING_ID", '"' + GA_TrackingId_Release + '"'

            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'

            if (signingConfigs.release.storeFile.exists()) {
                signingConfig signingConfigs.release
            } else {
                logger.info(
                        "keystore ${signingConfigs.release.storeFile} does not exist. Release signing is ignored.")
            }
        }
    }

    lintOptions {
        disable 'InvalidPackage'
        disable 'TypographyDashes'
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }
}


dependencies {
    compile 'com.android.support:appcompat-v7:23.2.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.android.support:design:23.2.1'
    compile 'com.android.support:support-annotations:23.2.1'
    compile 'com.google.android.gms:play-services-maps:8.4.0'
    compile 'com.google.android.gms:play-services-location:8.4.0'
    compile 'com.google.android.gms:play-services-analytics:8.4.0'
    compile 'com.google.android.gms:play-services-ads:8.4.0'
    compile 'com.google.code.findbugs:jsr305:3.0.1'
    compile 'io.reactivex:rxjava:1.1.2'
    compile 'com.cookpad.android.rxt4a:rxt4a:1.0.0'
    compile 'com.google.code.gson:gson:2.6.2'
    compile 'com.jakewharton.timber:timber:3.1.0'
    compile 'com.google.dagger:dagger:2.0.2'
    apt 'com.google.dagger:dagger-compiler:2.0.2'
    provided 'javax.annotation:jsr250-api:1.0'
    compile 'com.github.hotchemi:permissionsdispatcher:2.1.1'
    apt 'com.github.hotchemi:permissionsdispatcher-processor:2.1.1'
    debugCompile 'com.facebook.stetho:stetho:1.3.0'
    testCompile 'org.robolectric:robolectric:3.0'
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
}
