apply plugin: 'android'

dependencies {
    compile "com.jakewharton:butterknife:$butterknifeVersion"
    compile "com.google.android.gms:play-services:$playServicesVersion"
    compile "com.android.support:appcompat-v7:$supportPackageVersion"
}


def writeApiKeys(HashMap<String, String> dict) {
    def s = ""
    s += """<?xml version="1.0" encoding="utf-8"?>\n<resources>\n"""
    dict.each { key, value ->
        s += """<string name="$key">$value</string>\n"""
    }
    s += """</resources>\n"""

    def file = project.file("src/main/res/values/_api_keys.xml")
    logger.info("writing $file ...")
    file.write(s)
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.1"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        encoding = "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }

        debug {
            packageNameSuffix ".debug"
            versionNameSuffix "-debug"
        }
    }
}

android.applicationVariants.all { variant ->
    variant.mergeResources.dependsOn {
        switch (variant.buildType.name) {
            case "release":
                writeApiKeys("GoogleApiKey" : GoogleApiKey_Release)
                break
            case "debug":
                writeApiKeys("GoogleApiKey" : GoogleApiKey_Debug)
                break
        }
    }
}